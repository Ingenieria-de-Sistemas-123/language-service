plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.6'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
	id 'com.diffplug.spotless' version '6.25.0'
	id 'maven-publish'
	id 'org.jetbrains.kotlin.jvm'
}

group = 'com.snippetsearcher'
version = '0.0.1-SNAPSHOT'
description = 'PrintScript language API (validate/run/format/lint)'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
	maven {
		url = uri("https://maven.pkg.github.com/Ingenieria-de-Sistemas-123/PrintScript")
		credentials {
			username = findProperty("gpr.user")
					?: System.getenv("GPR_USER")
					?: System.getenv("GITHUB_ACTOR")
			password = findProperty("gpr.key")
					?: System.getenv("GPR_KEY")
					?: System.getenv("GITHUB_TOKEN")
		}
	}
}

dependencies {
	implementation 'info.picocli:picocli:4.7.6'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	// PrintScript toolchain
	implementation 'org.printscript:cli:1.5'
	implementation 'org.printscript:linter:1.6'
	implementation 'org.printscript:runner:1.6'
	implementation 'org.printscript:common:1.2'
	implementation 'org.printscript:lexer:1.6'
	implementation 'org.printscript:parser:1.5'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

tasks.named('test') {
	useJUnitPlatform()
}


spotless {
	java {
		target '**/*.java'
		googleJavaFormat()
		trimTrailingWhitespace()
		endWithNewline()
		indentWithSpaces(4)
	}
}

tasks.withType(Test).configureEach {
	useJUnitPlatform()
	finalizedBy 'jacocoTestReport'
}

jacoco {
	toolVersion = '0.8.11'
}

if (!tasks.names.contains('jacocoTestReport')) {
	tasks.register('jacocoTestReport', JacocoReport) {
		dependsOn 'test'
		reports {
			xml.required.set(true)
			html.required.set(true)
		}
		executionData.from fileTree(buildDir) {
			include 'jacoco/test.exec',
					'jacoco/test*.exec',
					'outputs/unit_test_code_coverage/*/*.ec'
		}
	}
} else {
	tasks.named('jacocoTestReport', JacocoReport).configure {
		reports {
			xml.required.set(true)
			html.required.set(true)
		}
		executionData.from fileTree(buildDir) {
			include 'jacoco/test.exec',
					'jacoco/test*.exec',
					'outputs/unit_test_code_coverage/*/*.ec'
		}
	}
}

if (!tasks.names.contains('jacocoTestCoverageVerification')) {
	tasks.register('jacocoTestCoverageVerification', JacocoCoverageVerification) {
		dependsOn 'test'
		violationRules {
			rule {
				limit {
					minimum = 0.00
				}
			}
		}
		executionData.from fileTree(buildDir) {
			include 'jacoco/test.exec',
					'jacoco/test*.exec',
					'outputs/unit_test_code_coverage/*/*.ec'
		}
	}
} else {
	tasks.named('jacocoTestCoverageVerification', JacocoCoverageVerification).configure {
		violationRules {
			rule {
				limit {
					minimum = 0.00
				}
			}
		}
		executionData.from fileTree(buildDir) {
			include 'jacoco/test.exec',
					'jacoco/test*.exec',
					'outputs/unit_test_code_coverage/*/*.ec'
		}
	}
}


tasks.named('check') {
	dependsOn 'spotlessCheck', 'jacocoTestReport', 'jacocoTestCoverageVerification'
}

publishing {
	publications {
		maven(MavenPublication) {
			from components.java
		}
	}
	repositories {
		maven {
			name = 'GitHubPackages'
			url = uri('https://maven.pkg.github.com/Ingenieria-de-Sistemas-123/PrintScript')
			credentials {
				username = System.getenv('GITHUB_ACTOR') ?: ''
				password = System.getenv('GITHUB_TOKEN') ?: ''
			}
		}
	}
}
